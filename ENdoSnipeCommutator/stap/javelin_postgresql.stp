/**
 * ENdoSnipe Commutator
 * PostgreSQLからEndoSnipeのDataCollectorに送る情報を収集する。
 *
 * Copyright (C) Acroquest Technology, Co.LTD
 */

%{
#include <linux/sched.h>
#include <string.h>
%}

function local_clock:long()
%{
    THIS->__retvalue = local_clock();
%}


global mypid
global utime, stime
global file_read, file_write
global net_receive, net_transmit
global sleep_start, sleep_time
global lock_start, lock_wait
global lock_num

global select_count, insert_count, update_count, delete_count, other_count
global query_parse, query_plan, query_rewrite, query_execute
global sql_exec_times
global sql_exec_time_threshold
global sql_over_threshold_count

global transaction_commit_count
global transaction_start
global transaction_time

global buffer_cp, clog_cp, multixact_cp, subtrans_cp, twophase_cp
global checkpoint_times

global wal_write_start
global wal_write_time

global lock_wait_start
global lock_wait_time

probe begin
{
    mypid = pid()
    lock_num = 10	// WAKEが先に来た時、配列のインデックスが負数にならないように。
    lock_start[lock_num] = 0
    select_count = 0
    insert_count = 0
    update_count = 0
    delete_count = 0
    other_count = 0
    transaction_commit_count = 0
    sql_exec_time_threshold = 1	//SQL実行時間の閾値(この時間をオーバーしたクエリの実行回数が出力される)
    sql_over_threshold_count = 0
    init_sql_exec_times()
    init_transaction_time()
    init_checkpoint_times()
    init_wal_write_time()
    init_lock_wait_time()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__start")
{
    query = user_string($arg1)
    if(strcmp_head6(query, "SELECT")){
        select_count++
    }else if(strcmp_head6(query, "INSERT")){
        insert_count++
    }else if(strcmp_head6(query, "UPDATE")){
        update_count++
    }else if(strcmp_head6(query, "DELETE")){
        delete_count++
    }else{
        other_count++
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__parse__start")
{
    query_parse[tid()] = local_clock() 
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__parse__done")
{
    start_clock = query_parse[tid()]; delete query_parse[tid()]
    if(start_clock){
        sql_exec_time = local_clock() - start_clock
        sql_exec_times["parse"] += sql_exec_time
        check_sql_exec_time(sql_exec_time)
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__plan__start")
{
    query_plan[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__plan__done")
{
    start_clock = query_plan[tid()]; delete query_plan[tid()]
    if(start_clock){
        sql_exec_time = local_clock() - start_clock
        sql_exec_times["plan"] += sql_exec_time
        check_sql_exec_time(sql_exec_time)
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__rewrite__start")
{
    query_rewrite[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__rewrite__done")
{
    start_clock = query_rewrite[tid()]; delete query_rewrite[tid()]
    if(start_clock){
        sql_exec_time = local_clock() - start_clock
        sql_exec_times["rewrite"] += sql_exec_time
        check_sql_exec_time(sql_exec_time)
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__execute__start")
{
    query_execute[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("query__execute__done")
{
    start_clock = query_execute[tid()]; delete query_execute[tid()]
    if(start_clock){
        sql_exec_time = local_clock() - start_clock
        sql_exec_times["execute"] += sql_exec_time
        check_sql_exec_time(sql_exec_time)
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("transaction__start")
{
    transaction_start[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("transaction__abort")
{
    start_clock = transaction_start[tid()]; delete transaction_start[tid()]
    if(start_clock){
        transaction_time += local_clock() - start_clock
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("transaction__commit")
{
    transaction_commit_count++

    start_clock = transaction_start[tid()]; delete transaction_start[tid()]
    if(start_clock){
        transaction_time += local_clock() - start_clock
    }

}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("buffer__checkpoint__start")
{
    buffer_cp[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("buffer__checkpoint__done")
{
    start_clock = buffer_cp[tid()]; delete buffer_cp[tid()]
    if(start_clock){
        checkpoint_times["buffer"] += local_clock() - start_clock
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("clog__checkpoint__start")
{
    clog_cp[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("clog__checkpoint__done")
{
    start_clock = clog_cp[tid()]; delete clog_cp[tid()]
    if(start_clock){
        checkpoint_times["clog"] += local_clock() - start_clock
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("multixact__checkpoint__start")
{
    multixact_cp[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("multixact__checkpoint__done")
{
    start_clock = multixact_cp[tid()]; delete multixact_cp[tid()]
    if(start_clock){
        checkpoint_times["multixact"] += local_clock() - start_clock
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("subtrans__checkpoint__start")
{
    subtrans_cp[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("subtrans__checkpoint__done")
{
    start_clock = subtrans_cp[tid()]; delete subtrans_cp[tid()]
    if(start_clock){
        checkpoint_times["subtrans"] += local_clock() - start_clock
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("twophase__checkpoint__start")
{
    twophase_cp[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("twophase__checkpoint__done")
{
    start_clock = twophase_cp[tid()]; delete twophase_cp[tid()]
    if(start_clock){
        checkpoint_times["twophase"] += local_clock() - start_clock
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("wal__buffer__write__dirty__start")
{
    wal_write_start[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("wal__buffer__write__dirty__done")
{
    start_clock = wal_write_start[tid()]; delete wal_write_start[tid()]
    if(start_clock){
        wal_write_time += local_clock() - start_clock
    }
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("lock__wait__start")
{
    lock_wait_start[tid()] = local_clock()
}

probe process("/usr/local/pgsql-9.2.4/bin/postgres").mark("lock__wait__done")
{
    start_clock = lock_wait_start[tid()]; delete lock_wait_start[tid()]
    if(start_clock){
        lock_wait_time += local_clock() - start_clock
    }
}

probe _ens.vfs_read.return = kernel.function("vfs_read").return
{
    if(mypid != pid()){
        file_read += $return
    }
}

probe _ens.vfs_write.return = kernel.function("vfs_write").return
{
    if(mypid != pid()){
        file_write += $return
    }
}

probe _ens.netdev.receive = kernel.function("netif_receive_skb")
{
    net_receive += $skb->len
}
probe _ens.tcp_recvmsg.return = kernel.function("tcp_recvmsg").return
{
    if(pid() == target()){
        net_receive += $return
    }
}
probe _ens.udp_recvmsg.return = kernel.function("udp_recvmsg").return
{
    if(pid() == target()){
        net_receive += $return
    }
}
probe _ens.sys_recvfrom.return = kernel.function("sys_recvfrom").return
{
}

probe _ens.netdev.transmit = kernel.function("dev_queue_xmit")
{
    if(pid() == target()){
        net_transmit += $skb->len
    }
}

probe _ens.sys_nanosleep = kernel.function("sys_nanosleep").call
{
    sleep_nanosec += ($rqtp->tv_sec * 1000000000 + $rqtp->tv_nsec)
    sleep_start[tid()] = local_clock()
}

probe _ens.sys_nanosleep.return = kernel.function("sys_nanosleep").return
{
    sleep_time += (local_clock() - sleep_start[tid()])
}

probe _ens.sys_futex = kernel.function("sys_futex").call
{
    flag = $op & 0x0F
    if ( 0 == flag || 9 == flag ){ /* FUTEX_WAIT or FUTEX_WAIT_BITSET */
        lock_start[lock_num] = local_clock()
        lock_num += 1
    }
    if ( 1 == flag || 10 == flag ){ /* FUTEX_WAKE or FUTEX_WAKE_BITSET */
        lock_num -= 1 /* WAIT時に保管した値を見る。なかったとしても次のWAIT時の保管先がそこになるだけ。 */
        if(0 < lock_start[lock_num]){ /* WAKEが先に来る時がある */
            lock_wait += (local_clock() - lock_start[lock_num])
        }
    }
}

probe _ens.sched_switch = kernel.trace("sched_switch")
{
    utime[tid()] = task_utime()
    stime[tid()] = task_stime()
}

probe _ens.vfs_read.return{}
probe _ens.vfs_write.return{}
probe _ens.netdev.receive{}
probe _ens.tcp_recvmsg.return{}
probe _ens.udp_recvmsg.return{
    print_backtrace()
}
probe _ens.netdev.transmit{}
probe _ens.sys_nanosleep{}
probe _ens.sys_nanosleep.return{}
probe _ens.sys_futex{}
probe _ens.sched_switch{}
//probe _ens.sys_recvfrom.return{}


function print_period()
{
    printf("/common/fundamental/time/current,%ld\n",gettimeofday_ms())

    foreach ([tid] in utime) {
        utimemerged += utime[tid]
    }
    foreach ([tid] in stime) {
        stimemerged += stime[tid]
    }
    delete utime
    delete stime
    printf("/javelin/process/cpu/time/total,%ld\n", utimemerged + stimemerged)
    printf("/javelin/system/cpu/time/user,%ld\n", utimemerged)
}

function print_use_memory()
{
    printf("/javelin/process/memory/physical/used:bytes,%ld\n", proc_mem_rss_pid(target()) * mem_page_size())
}

function print_vfs_read()
{
    printf("/javelin/system/file/read:bytes,%ld\n",file_read)
    delete file_read
}

function print_vfs_write()
{
    printf("/javelin/system/file/write:bytes,%ld\n",file_write)
    delete file_write
}

function print_net_receive()
{
    printf("/javelin/proc/network/input/used:bytes,%ld\n",net_receive)
    delete net_receive
}

function print_net_transmit()
{
    printf("/javelin/proc/network/output/used:bytes,%ld\n",net_transmit)
    delete net_transmit
}

function print_nanosleep()
{
    printf("/javelin/process/cpu/sleep/total,%ld\n", sleep_time)
    delete sleep_time
}

function print_lockwait()
{
    printf("/javelin/process/wait/lock/total,%ld\n", lock_wait)
    delete lock_wait
    delete lock_start
    lock_num = 10
}

function print_query_counts()
{
    query_count = select_count + insert_count + update_count + delete_count + other_count
    printf("/postgresql/sql,%d\n", query_count)
    printf("/postgresql/sql/select,%d\n", select_count)
    printf("/postgresql/sql/insert,%d\n", insert_count)
    printf("/postgresql/sql/update,%d\n", update_count)
    printf("/postgresql/sql/delete,%d\n", delete_count)
    printf("/postgresql/sql/other,%d\n", other_count)
}

function print_transaction_count()
{
    printf("/postgresql/transaction/count,%d\n", transaction_commit_count)
}

function print_sql_exec_times()
{
    printf("/postgresql/sql_time/parse,%ld\n", sql_exec_times["parse"])
    printf("/postgresql/sql_time/plan,%ld\n", sql_exec_times["plan"])
    printf("/postgresql/sql_time/rewrite,%ld\n", sql_exec_times["rewrite"])
    printf("/postgresql/sql_time/execute,%ld\n", sql_exec_times["execute"])
}

function print_transaction_time()
{
    printf("/postgresql/transaction/time,%ld\n", transaction_time)
}

function print_checkpoint_times()
{
    printf("/postgresql/checkpoint_time/buffer,%ld\n", checkpoint_times["buffer"])
    printf("/postgresql/checkpoint_time/clog,%ld\n", checkpoint_times["clog"])
    printf("/postgresql/checkpoint_time/multixact,%ld\n", checkpoint_times["multixact"])
    printf("/postgresql/checkpoint_time/subtrans,%ld\n", checkpoint_times["subtrans"])
    printf("/postgresql/checkpoint_time/twophase,%ld\n", checkpoint_times["twophase"])
}

function print_wal_write_time()
{
    printf("/postgresql/wal_write_time,%ld\n", wal_write_time)
}

function print_sql_over_threshold_count()
{
    printf("/postgresql/sql_over_threshold,%ld\n", sql_over_threshold_count)
}

function print_lock_wait_time()
{
    printf("/postgresql/lock_wait_time,%ld\n", lock_wait_time)
}

function init_sql_exec_times()
{
    sql_exec_times["parse"] = 0
    sql_exec_times["plan"] = 0
    sql_exec_times["rewrite"] = 0
    sql_exec_times["execute"] = 0
}

function init_transaction_time()
{
    transaction_time = 0
}

function init_checkpoint_times()
{
    checkpoint_times["buffer"] = 0
    checkpoint_times["clog"] = 0
    checkpoint_times["multixact"] = 0
    checkpoint_times["subtrans"] = 0
    checkpoint_times["twophase"] = 0
}

function init_wal_write_time()
{
    wal_write_time = 0
}

function init_lock_wait_time()
{
    lock_wait_time = 0
}

function check_sql_exec_time(s1: long)
{
    if( s1 <= sql_exec_time_threshold ) return
    sql_over_threshold_count++
}

function strcmp_head6:long(s1: string, s2: string){
    for( i=0; i<6; i++ ){
        if( !strcmp_ignorecase( substr(s1, i, i+1), substr(s2, i, i+1) ) ) return 0;
    }
    return 1
}

function strcmp_ignorecase:long(s1: string, s2: string)%{
    char c1, c2;
    int i1, i2;
    c1 = STAP_ARG_s1[0];
    c2 = STAP_ARG_s2[0];
    i1 = toupper(c1);
    i2 = toupper(c2);
    if( i1 == i2 ) STAP_RETVALUE = 1;
    else STAP_RETVALUE = 0;
%}

probe timer.ms(15000)
{
    print_period()
    print_use_memory()
    print_vfs_read()
    print_vfs_write()
    print_net_receive()
    print_net_transmit()
    print_nanosleep()
    print_lockwait()
    print_query_counts()
    print_sql_exec_times()
    print_transaction_count()
    print_transaction_time()
    print_checkpoint_times()
    print_wal_write_time()
    print_sql_over_threshold_count()
    print_lock_wait_time()
    init_sql_exec_times()
    init_transaction_time()
    init_checkpoint_times()
    init_wal_write_time()
    printf(".\n");
}

